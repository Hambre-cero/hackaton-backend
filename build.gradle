buildscript {

    ext {
        springBootVersion = '2.5.2'
        lombokVersion = '5.3.0'
        spotbugsVersion = '4.7.1'
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "io.freefair.gradle:lombok-plugin:$lombokVersion"
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:$spotbugsVersion"
    }
}

subprojects {

    group 'co.programacionmaster'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    dependencies {
        implementation 'io.vavr:vavr:0.10.4'
        implementation 'com.google.guava:guava:27.1-jre'
        implementation 'com.neovisionaries:nv-i18n:1.26'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    pmd {
        ruleSetFiles = files("${rootDir}/configuration/pmd-custom-rules.xml")
        ruleSets = []
    }

    checkstyle {
        configFile = file("${rootDir}/configuration/google_checks.xml")
    }

    spotbugs {
        excludeFilter = file("${rootDir}/configuration/spotbugs-exclude.xml")
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}

